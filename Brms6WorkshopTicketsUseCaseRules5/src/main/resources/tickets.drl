package org.jboss.ddoyle.brms.workshop

//list any import classes here.

import org.jboss.ddoyle.brms.workshop.model.Event
import org.jboss.ddoyle.brms.workshop.model.EventPass
import org.jboss.ddoyle.brms.workshop.model.IsAdult
import org.jboss.ddoyle.brms.workshop.model.Person
import org.jboss.ddoyle.brms.workshop.model.Ticket
import org.jboss.ddoyle.brms.workshop.model.TicketOffer
import org.jboss.ddoyle.brms.workshop.model.TicketOffers
import org.jboss.ddoyle.brms.workshop.model.Venue

//declare any global variables here
global org.jboss.ddoyle.brms.workshop.model.TicketOffers ticketOffers;


/*
	Concepts: Tickets of type D cannot be sold to non-adults.
	Non-adults get a 25% discount on tickets.
	Persons with a special event pass get 10% discount on top of all discounts.
	Golden tickets can only be sold to people with an Event pass.
*/

/*
 The idea is to use a flow of rules

 Technical Rules (assess whether someone is an adult) => Filter (filters tickets we can't sell to this person) -> Create TicketOffers with tickets that all stil there -> Apply discounts on the offers -> Apply additional discounts because of a special card,

 Changing the ticket offers can cause rules to fire again, so this might be a nice use-case.
*/

//Check whether person is adultt
rule "Is Adult"
salience 200
when 
	$p: Person(age >=18)
then
	insert(new IsAdult($p));
end

rule "Filter Tickets Adults"
salience 100
when
	$p:Person()
	not(exists(IsAdult()))
	$t:Ticket(event.rating == Event.EVENT_RATING.D)
then
	retract($t)
end

rule "Filter Golden tickets"
salience 75
when
	$p:Person()
	not (exists (EventPass(person == $p)))
	$t: Ticket(ticketClass == Ticket.TICKET_CLASS.GOLD)
then
	retract($t)
end

rule "Create TicketOffers"
salience 50
when 
	$t: Ticket() 
then 
	TicketOffer tOffer = new TicketOffer($t);
	ticketOffers.addTicket(tOffer);
	insert(tOffer);
end

rule "Discount Agenda-Group setup"
salience 30
when
	Person();
then
	kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("discount").setFocus();
end

rule "25% discount for non-adults"
agenda-group "discount"
lock-on-active
when
	$p:Person()
	not(exists(IsAdult()))
	$t: TicketOffer()
then
	modify($t) {
		addDiscount(25);	
	}
end

rule "EventPass discount"
agenda-group "discount"
lock-on-active
when
	$p:Person()
	EventPass(person ==$p)
	$t: TicketOffer()
then
	modify($t) {
		addDiscount(10);
	}
end